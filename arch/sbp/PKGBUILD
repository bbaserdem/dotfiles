# Maintainer: Batuhan Baserdem <baserdem.batuhan@gmail.com>
pkgbase=sbp
pkgname=(
    'sbp-base'
    'sbp-gui'
    'sbp-extra'
    'sbp-efi'
    'sbp-xfce'
    'sbp-sway'
    'sbp-i3'
    'sbp-server'
    'sbp-laptop'
    'sbp-notebook'
    'sbp-workstation'
    )
pkgver=0.9
pkgrel=67
pkgdesc="Meta packages to automate packages computer systems"
arch=('any')
url="https://github.com/bbaserdem/dotfiles"
licence=(MIT)
groups=(bbaserdem)
source=(
    'base.bt-polkit'
    'base.btaudio'
    'base.eth'
    'base.fixed-bitmap'
    'base.inotify'
    'base.install'
    'base.nftables'
    'base.pa_usb'
    'base.reflector.service'
    'base.reflector.timer'
    'base.snapper-root'
    'base.sudoers'
    'base.sysctl'
    'base.tether'
    'base.touchpad'
    'base.updater'
    'base.vconsole'
    'base.wifi'
    'base.zshenv'
    'gui.install'
    'extra.install'
    'extra.qbupdate'
    'efi.conf'
    'efi.kernel-conf'
    'efi.refind'
    'efi.refind-hook'
    'efi.signscript'
    'efi.stable'
    'laptop.backlight'
    'laptop.battery'
    'laptop.install'
    'laptop.network'
    'notebook.install'
    'notebook.network'
    'server.install'
    'workstation.install'
    'workstation.network'
    'xfce.install'
    'xfce.nm-polkit'
    'xfce.wallpaper'
    )
md5sums=('0d3afa7b391a50313df9449b80240807'
         '2adc9c1077ee29381b6ffedf871744f4'
         '5b3f3cc9194f11a91c4d10119bdfd656'
         '9806f3d492c6e42f3c8d44f3f5cd7d52'
         'aa695fb291e6b908235fe2570b0e9877'
         'd917ff1b214b20493a23ddf29341101c'
         '2b4a7715d47beadb71db5f7b3369a4c9'
         '98b541ce9c3fd6fff43d8d4ae0762215'
         '7a1ff93a4e176d816c3ab6b5ea667ca0'
         'c972c1e5c236f6e3c0f7d1b4989ebb75'
         '00e5bc63b7bb82526516df8f45c4745c'
         'df0911ed7f3bbf285de4e3c08662dafd'
         '586c14c5f87f0097e0dfcb0454bfc2a6'
         '4e8599e6d74c8d019edccbb09faf392b'
         '18c4e485034ee909f9d4f3c7fab3954f'
         '3ad35f4045a3fce5bbfeecd0602c3cee'
         '1ddf47f1278958e4cc65bfbd96dbd6f7'
         'e71265ed72b65b8fb8092d6a4da0da3a'
         '7128851ee00d22284cbdbe9b8257ad14'
         '61f611b56f9df32bb71896d6d8ca394a'
         'b19954dbb641caa1328725b405b49130'
         '17a7cda5f070fb76e1070a2f1fcbc21d'
         'a7c1283261fb4b3e77efa739c81f481d'
         '79267b7f57a008bdc8eadb1f399e41c5'
         '7084c59993679d938c4265818f2116a4'
         'ef83413fe51d5f37999b2ea8ef231d87'
         'a73c16867d95edef046d2b99b90af986'
         'e3217b4c75d3bc302347b7bdf27f29de'
         '36adcc034c7a69ffe93e2c0937f051da'
         '31bc6b40fff742f9281088c7acdb6feb'
         'd35cb2e4bbf1e8adace00795875cab43'
         '963647fe81d1824bb977c3618d56959a'
         '850b04cc7eb552489721cd72ed945634'
         '7d625c54c37c6296f2ab1bbebbeb8308'
         '82d5373c4cd8d047577adcde389de4ba'
         '93093b7e474b1d8204f90edf891e3dd0'
         '7d432ab4dd0cbb05cc1399e8cd02cfbe'
         '20e80f330379b2036aa100692a47d004'
         'a470b666e3e30ac281ff74779b7c5cd6'
         '83e1b805fc92b3d1d3faf65158448687')
validpgpkeys=(
    '0B7151C823559DD8A7A04CE36426139E2F4C6CCE')

package_sbp-base() {
    provides=(vim vi)
    conflicts=(vim vi)
    install=base.install

    # Base and base-devel group
    depends=(
        'bash'          'bzip2'         'coreutils'     'cryptsetup'
        'device-mapper' 'dhcpcd'        'diffutils'     'e2fsprogs'
        'file'          'filesystem'    'findutils'     'gawk'
        'gcc-libs'      'gettext'       'glibc'         'grep'
        'gzip'          'inetutils'     'iproute2'      'iputils'
        'jfsutils'      'less'          'licenses'      'linux'
        'logrotate'     'lvm2'          'man-db'        'man-pages'
        'mdadm'         'pacman'        'pciutils'      'perl'
        'procps-ng'     'psmisc'        's-nail'        'sed'
        'shadow'        'tar'           'sysfsutils'    'systemd-sysvcompat'
        'texinfo'       'usbutils'      'util-linux'    'which'     'xfsprogs'
        'autoconf'      'automake'      'binutils'      'bison'
        'fakeroot'      'file'          'findutils'     'flex'
        'gawk'          'gcc'           'gettext'       'grep'
        'groff'         'gzip'          'libtool'       'm4'
        'make'          'pacman'        'patch'         'pkgconf'
        'sed'           'sudo'          'systemd'       'texinfo'
        'util-linux'    'which'         'btrfs-progs'   'f2fs-tools'
        'sudo'                                  # To check your privilage
        'exfat-utils' 'udftools'                # File system stuff
        'bridge-utils' 'nftables' 'hostapd'     # Networking stuff
        )
    # Utilities and Functionality
    depends+=(
        'archiso'                               # Live USB tool
        'alsa-utils' 'lib32-alsa-plugins'       # ALSA
        'pulseaudio' 'lib32-libpulse' 'pamixer' # Pulseaudio
        'pulseaudio-alsa'                       # Pulse plugins
        'bluez' 'bluez-libs'                    # Bluetooth
        'bluez-utils' 'pulseaudio-bluetooth'
        'clonezilla'                            # Hard disk cloning
        'ctags'                                 # Tag generator
        'cups' 'cups-pdf' 'cups-pk-helper'      # Printing
        'abcde' 'cdparanoia'                    # CD utility
        'amd-ucode' 'intel-ucode'               # Microcode updates
        'asp' 'devtools'                        # Arch devtools
        'reflector' 'aurutils' 'pacman-contrib' # Package management
        'atool' 'bzip2' 'cpio' 'gzip' 'lhasa'   # Archive tools
        'lzop' 'p7zip' 'tar' 'unace' 'unrar' 'unzip' 'xz' 'zip'
        'bash-completion'                       # Completion for bash
        'cpupower' 'rasdaemon'                  # CPU supervisor, AUR - MCE
        'discount' 'jq' 'yajl'                  # Markdown & JSON
        'flatpak'                               # Jailed software repository
        'geoip' 'geoip-database-extra'          # IP to location resolver
        'geoclue2'                              # GPS
        'git'                                   # Version control tool
        'gnupg' 'sshfs' 'openssh' 'fuse3' 'tor' # GPG and SSH
        'zsh' 'grml-zsh-config'                 # Improved shell
        'imagemagick'                           # Commandline image manipulator
        'inotify-tools' 'parted'                # File change watching & part.
        'languagetool'                          # Grammar checker
        'linux-firmware' 'linux-headers'        # Firmware for the Linux kernel
        'lm_sensors'                            # Thermal sensors
        'msmtp' 'msmtp-mta' 'isync'             # Mail and DAV stuff
        'notmuch' 'vdirsyncer'
        'openconnect' 'openvpn'                 # VPN services
        'private-internet-access-vpn'           # AUR: Set up PIA
        'pass' 'qrencode'                       # Password manager
        'rsync'                                 # File synching utility
        'snapper'                               # BTRFS snapshot manager
        'tmux'                                  # Terminal multiplexer
        'tlp' 'stress'                          # Power manager
        'iwd' 'ppp' 'pptpclient'                # INet Wireless Daemon
        'networkmanager'                        # Network connection manager
        'mpc' 'mpd' 'mpdscribble'               # Music daemon and AUR scrobbler
        'syncthing' 'w3m' 'youtube-dl'          # Internet tools
        'tzupdate'                              # AUR: For autoupdating time
        'xkb-switch'                            # AUR: Manage keyboard states
        'xdg-user-dirs'                         # Manage user directories
        )
    # Console apps and utilities
    depends+=(
        'htop'                                      # Power sources
        'khal'                                      # Calendar
        'khard'                                     # Contacts
        'ncmpcpp'                                   # Music player
        'neofetch'                                  # System info printer
        'neomutt'                                   # Mail reader
        'neovim' 'python-neovim' 'python2-neovim'   # Text editor
        'newsboat'                                  # RSS/Atom feed
        'pulsemixer'                                # Pulse audio mixer
        'ranger' 'vifm'                             # File browser
        'todoman'                                   # Tasks manager
        'weechat'                                   # IRC client
        )
    # Dev stuff
    depends+=(
        'arduino' 'arduino-avr-core'            # Arduino stuff
        'arduino-docs' 'arm' 'arm-none-eabi-binutils' 'arm-none-eabi-gcc'
        'arm-none-eabi-newlib' 'avr-binutils' 'avr-gcc' 'avr-libc'
        'avrdude' 'dfu-util' 'dfu-programmer'
        'android-tools' 'android-udev' 'libmtp' # Android
        's3cmd'                                 # Amazon bucket console
        'clang' 'cmake'                         # C
        'bpython' 'jupyter-nbconvert'           # Python
        'jupyter-notebook' 'lapack' 'mathjax' 'openblas' 'python-matplotlib'
        'python-sympy' 'python-pandas' 'python-pexpect' 'python-scikit-learn'
        'python-ipykernel' 'python-neovim' 'python-numpy' 'python-virtualenv'
        'python-xdg' 'python2-pip' 'python-pip' 'python-jedi' 'python-magic'
        'jre8-openjdk' 'jre10-openjdk'          # Java
        'gcc6'                                  # For matlab
        )
    install -Dm 0644 base.bt-polkit         "${pkgdir}/etc/polkit-1/rules.d/51-blueman.rules"
    install -Dm 0644 base.btaudio           "${pkgdir}/etc/bluetooth/audio.conf"
    install -Dm 0644 base.eth               "${pkgdir}/etc/systemd/network/21-wired.network"
    install -Dm 0644 base.fixed-bitmap      "${pkgdir}/etc/fonts/conf.avail/90-fixed-bitmaps.conf"
    install -Dm 0644 base.inotify           "${pkgdir}/etc/sysctl.d/01-inotify.conf"
    install -Dm 0644 base.nftables          "${pkgdir}/etc/nftables.conf.sbp"
    install -Dm 0644 base.pa_usb            "${pkgdir}/etc/udev/rules.d/89-pulseaudio-usb.rules"
    install -Dm 0644 base.reflector.service "${pkgdir}/etc/systemd/system/reflector.service"
    install -Dm 0644 base.reflector.timer   "${pkgdir}/etc/systemd/system/reflector.timer"
    install -Dm 0644 base.snapper-root      "${pkgdir}/etc/snapper/configs/root"
    install -Dm 0750 base.sudoers           "${pkgdir}/etc/sudoers.d/sbp-base"
    install -Dm 0644 base.sysctl            "${pkgdir}/etc/sysctl.d/01-sysctl.conf"
    install -Dm 0644 base.tether            "${pkgdir}/etc/systemd/network/23-tether.network"
    install -Dm 0644 base.touchpad          "${pkgdir}/etc/X11/xorg.conf.d/30-touchpad.conf"
    install -Dm 0644 base.updater           "${pkgdir}/etc/systemd/system/pacman-update.service"
    install -Dm 0644 base.vconsole          "${pkgdir}/etc/vconsole.conf"
    install -Dm 0644 base.wifi              "${pkgdir}/etc/systemd/network/22-wireless.network"
    install -Dm 0644 base.zshenv            "${pkgdir}/etc/zsh/zshenv"
    # Make it so nano, vi and vim points to neovim
    install -dm 0755 "${pkgdir}/usr/bin"
    ln -sf /usr/bin/nvim "${pkgdir}/usr/bin/vim"
    ln -sf /usr/bin/nvim "${pkgdir}/usr/bin/nano"
    ln -sf /usr/bin/nvim "${pkgdir}/usr/bin/vi"
}

# Packages needed for a working gui
package_sbp-gui() {
    install=gui.install
    # X-org
    depends=(
        'xorgproto'             'xf86-video-vesa'       'xorg-bdftopcf'
        'xorg-docs'             'xorg-font-util'        'xorg-fonts-100dpi'
        'xorg-fonts-75dpi'      'xorg-fonts-encodings'  'xorg-iceauth'
        'xorg-luit'             'xorg-mkfontdir'        'xorg-mkfontscale'
        'xorg-server'           'xorg-server-common'    'xorg-server-devel'
        'xorg-server-xdmx'      'xorg-server-xephyr'    'xorg-server-xnest'
        'xorg-server-xvfb'      'xorg-server-xwayland'  'xorg-sessreg'
        'xorg-setxkbmap'        'xorg-smproxy'          'xorg-x11perf'
        'xorg-xauth'            'xorg-xbacklight'       'xorg-xcmsdb'
        'xorg-xcursorgen'       'xorg-xdpyinfo'         'xorg-xdriinfo'
        'xorg-xev'              'xorg-xgamma'           'xorg-xhost'
        'xorg-xinput'           'xorg-xkbcomp'          'xorg-xkbevd'
        'xorg-xkbutils'         'xorg-xkill'            'xorg-xlsatoms'
        'xorg-xlsclients'       'xorg-xmodmap'          'xorg-xpr'
        'xorg-xprop'            'xorg-xrandr'           'xorg-xrdb'
        'xorg-xrefresh'         'xorg-xset'             'xorg-xsetroot'
        'xorg-xvinfo'           'xorg-xwd'              'xorg-xwininfo'
        'xorg-xwud'             'xf86-input-libinput'   'xorg-xinit'
        'xorg-fonts-misc'
        'xsel'                      # Select text/area
        'wayland'
        )
    # Login manager
    depends+=(
        'lightdm'
        'lightdm-gtk-greeter'
        'lightdm-gtk-greeter-settings'
        'lightdm-webkit2-greeter'
        'lightdm-webkit-theme-litarvan'
        )
    # Fonts, themes, icons etc.
    depends+=(
        'terminess-powerline-font-git'              # AUR: Console fonts
        'ttf-iosevka'                               # AUR: Statusline font
        'otf-nerd-fonts-fira-code'                  # AUR: Glyphs & Ligatures
        'otf-hasklig'                               # AUR: Font with ligatures
        'bdf-tewi-git'                              # AUR: Nice bitmap font
        'siji-git'                                  # AUR: Nice monospace font
        'gohufont'                                  # AUR: Nice bitmap font
        'tamsyn-font'                               # Nice monospace font
        'noto-fonts-emoji'                          # Emoji font
        'adobe-source-serif-pro-fonts'              # Nice serif font
        'ttf-droid'                                 # Default looking font
        )
    # Theming
    depends+=(
        'qt4'                                       # QT implementation
        'python-pyqt5'                              # QT stuff for python
        'archlinux-wallpaper' 'archlinux-artwork'   # AUR: Wallpapers for Arch
        'arc-gtk-theme'                             # GTK theme
        'arc-kde'                                   # QT theme
        'qt5-styleplugins'                          # QT GTK integration
        'papirus-icon-theme'                        # Nice icon theme
        'gnome-themes-extra'                        # Icon compatibility
        'gtk-engine-murrine'                        # Fallback icons
        'breeze-hacked-cursor-theme'                # Hacked cursor
        'xcursor-comix'                             # Comic cursor
    )
    # Some universally useful GUI programs
    depends+=(
        'blueman'                               # Bluetooth manager
        'cantata'                               # MPD frontend
        'firefox'                               # Fallback browser
        'gparted'                               # Graphical partition editor
        'gtk2fontsel' 'gucharmap'               # Font and character map
        'mpv' 'smplayer'                        # Mpv frontend
        'network-manager-applet' 'nm-connection-editor'
        'pavucontrol'                           # Pulse audio control menu
        'pdfsam'                                # Pdf manipulator
        'qemu' 'ovmf'                           # Virtualizer
        'redshift'                              # Screen dimmer
        'ristretto'                             # Image viewer
        'system-config-printer'                 # Printer setup utility
        'termite'                               # Wayland native terminal
        'thunar'                                # File browser
        'thunar-volman' 'gvfs' 'thunar-archive-plugin' 'xarchiver' 'file-roller'
        'thunar-media-tags-plugin' 'tumbler' 'raw-thumbnailer' 'udisks2'
        'vlc'                                   # Media player
        'zathura' 'zathura-cb' 'zathura-djvu'   # Document viewer
        'zathura-pdf-mupdf' 'zathura-ps'
    )
    # Some universal useful gui functionality
    depends+=(
        'freerdp'               # Remote desktop tool
        'nx3-compat'            # NoMachine desktop tool
        'gpicview'              # Picture to ascii
        'pdfjs'                 # PDF rendering to webpages
        'polkit' 'polkit-gnome' # Authentication
        'trezor-bridge-bin'     # Hardware wallet
    )
}

# Meant to be excluded from some setups due to size
package_sbp-extra() {
    depends=('sbp-base' 'sbp-gui')
    depends+=(
        'audacity'                              # Audio manipulator
        'blender'                               # 3d designing utility
        'cantata'                               # MPD frontend
        'd-feet'                                # D-Bus settings
        'darktable'                             # Raw format editor
        'gimp'                                  # GNU Image Manipulation
        'gitg'                                  # Git visualizer
        'gourmet'                               # Recipe tracker
        'inkscape'                              # Vector graphics editor
        'libreoffice-fresh'                     # Office suite
        'neovim-gtk-git'                        # AUR: Neovim gui
        'octave'                                # Octave
        'openscad'                              # CAD programming language
        'pamac-aur'                             # Package manager
        'picard'                                # Music tagger
        'qutebrowser'                           # Browser with vim binding
        'remmina'                               # Remote manager
        'rambox-bin'                            # AUR: Communication app
        'skypeforlinux-stable-bin'              # AUR: Skype app
        'spyder3'                               # Python dev environment
        'steam' 'steam-native-runtime' 'steam-fonts' 'steamos-compositor'
        'stellarium'                            # Star viewer
        'syncthing-gtk'                         # Tray icon for syncthing
        'texlive-core' 'texlive-science' 'biber' # Latex
        'texlive-fontsextra' 'texlive-latexextra' 'texlive-pictures' 'texlive-pstricks'
        'udiskie'                               # Mount manager with tray icon
        'wine' 'samba'                          # Windows compatibility
        )
    install -Dm 0644 extra.qbupdate "${pkgdir}/etc/pacman.d/hooks/99-qutebrowser.hook"
}

# Xfce-specific stuff
package_sbp-xfce() {
    install=xfce.install
    depends=('sbp-gui')
    depends+=(
        'exo'
        'firefox'
        'garcon'
        'gtk-xfce-engine'
        'mousepad'
        'rhythmbox'
        'thunderbird'
        'xfce4-appfinder'
	    'xfce4-artwork'
        'xfce4-battery-plugin'
        'xfce4-clipman-plugin'
        'xfce4-cpufreq-plugin'
        'xfce4-cpugraph-plugin'
        'xfce4-datetime-plugin'
        'xfce4-dict'
        'xfce4-diskperf-plugin'
        'xfce4-fsguard-plugin'
        'xfce4-genmon-plugin'
        'xfce4-mailwatch-plugin'
        'xfce4-mount-plugin'
        'xfce4-mpc-plugin'
        'xfce4-netload-plugin'
        'xfce4-notifyd'
        'xfce4-panel'
        'xfce4-power-manager'
        'xfce4-pulseaudio-plugin'
        'xfce4-screenshooter'
        'xfce4-sensors-plugin'
        'xfce4-session'
        'xfce4-settings'
        'xfce4-smartbookmark-plugin'
        'xfce4-systemload-plugin'
        'xfce4-taskmanager'
        'xfce4-terminal'
        'xfce4-time-out-plugin'
        'xfce4-timer-plugin'
        'xfce4-verve-plugin'
        'xfce4-wavelan-plugin'
        'xfce4-weather-plugin'
        'xfce4-whiskermenu-plugin'
        'xfce4-xkb-plugin'
        'xfconf'
        'xfdesktop'
        'xfwm4'
        'xfwm4-themes'
        )
    install -Dm 0644 xfce.nm-polkit "${pkgdir}/etc/polkit-1/rules.d/50-NetworkManager.rules"
    install -Dm 0755 xfce.wallpaper "${pkgdir}/usr/share/archlinux/wallpaper/archlinux-xfce4.jpg"
}

# i3 stuff
package_sbp-i3() {
    depends=('sbp-gui')
    # i3 stuff
    depends+=(
        'brightnessctl'                 # Brightness controls
        'dex'                           # Autostart launcher
        'dunst'                         # Notification daemon
        'feh'                           # Wallpaper management
        'i3-gaps'                       # Improved window manager
        'i3lock'                        # Screen locker
        'i3blocks'                      # Input to status-bar
        'maim'                          # Screenshot utility
        'rofi' 'rofi-pass' 'rofi-emoji' # Application launcher
        )
}

# Sway stuff
package_sbp-sway() {
    depends=('sbp-gui')
    # sway stuff
    depends+=(
        'brightnessctl'                 # Brightness controls
        'dex'                           # Autostart launcher
        'i3blocks'                      # Input to status-bar
        'mako-git'                      # Notification daemon
        'rofi' 'rofi-pass' 'rofi-emoji' # Application launcher
        'sway-git'                      # Wayland compositor
        'wlroots-git'                   # Wayland library
        )
}
#        'redshift-sway-git'             # Screen dimmer for wayland
#        'rofi-wayland-git'              # App launcher
package_sbp-efi() {
    depends=('refind-efi' 'efibootmgr' 'efivar' 'efitools' 'sbsigntools')
    install -Dm 0755 efi.conf           "${pkgdir}/efi/EFI/rEFInd/refind.conf.example"
    install -Dm 0755 efi.kernel-conf    "${pkgdir}/boot/refind_linux.conf.example"
    install -Dm 0755 efi.refind         "${pkgdir}/etc/sbkeys/update-refind.sh"
    install -Dm 0644 efi.refind-hook    "${pkgdir}/etc/pacman.d/hooks/12-refind.hook"
    install -Dm 0755 efi.signscript     "${pkgdir}/etc/sbkeys/sbsign.sh"
    install -Dm 0644 efi.stable         "${pkgdir}/etc/pacman.d/hooks/11-stable.hook"
}

# Server specific stuff
package_sbp-server() {
    depends=('sbp-base' 'sbp-efi' 'sbp-extra')
    conflicts=('sbp-workstation' 'sbp-notebook' 'sbp-laptop')
    install=server.install
    depends+=(
        'apache'            # Web server
        'dovecot'           # IMAP email server
        'kodi'              # Media file server
        'mariadb'           # Database management
        'postfix'           # Mail transfer agent
        'rspamd'            # Spam filter for mail
        'transmission-cli'  # Torrenting
        'radicale'          # CalDAV and CardDAV server
        )
}

# Laptop specific stuff; AMD
package_sbp-laptop() {
    depends=('sbp-base' 'sbp-gui' 'sbp-extra' 'sbp-efi')
    conflicts=('sbp-workstation' 'sbp-server' 'sbp-notebook')
    install=laptop.install
    depends+=(
        'mesa' 'lib32-mesa'
        'mesa-demos' 'lib32-mesa-demos' 
        'libva-mesa-driver' 'lib32-libva-mesa-driver'
        'mesa-vdpau' 'lib32-mesa-vdpau'
        'libva-vdpau-driver'
        'xf86-video-amdgpu'
        'vulkan-radeon'
        )
    install -Dm 0755 laptop.network   "${pkgdir}/etc/udev/rules.d/10-network.rules"
    install -Dm 0644 laptop.backlight "${pkgdir}/etc/udev/rules.d/90-backlight.rules"
    install -Dm 0644 laptop.battery   "${pkgdir}/etc/udev/rules.d/91-battery.rules"
}

# Notebook specific stuff; Nvidia
package_sbp-notebook() {
    depends=('sbp-base' 'sbp-gui' 'sbp-extra' 'sbp-efi')
    conflicts=('sbp-workstation' 'sbp-server' 'sbp-laptop')
    install=notebook.install
    depends+=(
        'bbswitch-dkms'
        'bumblebee'
        'cuda'
        'lib32-nvidia-utils'
        'lib32-virtualgl'
        'mesa'
        'mesa-demos'
        'nvdock-bumblebee'
        'nvidia-dkms'
        'nvidia-settings'
        'nvidia-utils'
        'ocl-icd'
        'opencl-nvidia'
        'primus'
        )
    install -Dm 0755 notebook.network "${pkgdir}/etc/udev/rules.d/10-network.rules"
}

# Work specific stuff
package_sbp-workstation() {
    depends=('sbp-base' 'sbp-extra' 'sbp-gui')
    conflicts=('sbp-server' 'sbp-notebook' 'sbp-laptop')
    install=workstation.install
    depends+=(
        'dropbox'       # AUR: File synching
        'grub' 'os-prober'
        'nvidia-340xx-dkms' 'nvidia-340xx-utils' 'lib32-nvidia-340xx-utils'
        'ocl-icd' 'opencl-nvidia-340xx' 'cuda'
        'libva-vdpau-driver'
        )
    install -Dm 0755 workstation.network "${pkgdir}/etc/udev/rules.d/10-network.rules"
}

